
# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

setopt autocd
setopt autopushd
setopt pushdminus

# Install zinit if not installed
if [ ! -d "${HOME}/.zinit" ]; then
  mkdir ${HOME}/.zinit
	git clone https://github.com/zdharma-continuum/zinit.git ${HOME}/.zinit/bin
	zcompile ${HOME}/.zinit/bin/zinit.zsh
fi

### Added by Zplugin's installer
source "${HOME}/.zinit/bin/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

### Functions to make zinit configuration less verbose
zpt() { zinit ice wait"${1}" lucid               "${@:2}"; } # Turbo
zpi() { zinit ice lucid                            "${@}"; } # Regular Ice
zp()  { [ -z $2 ] && zinit light "${@}" || zinit "${@}"; } # zinit

# Load nvm before sourcing other scripts
# no lazy (for now) in order for powerline to work as expected
# export NVM_LAZY_LOAD=true
export NODE_PATH=x
#zinit load lukechilds/zsh-nvm
#zinit wait lucid light-mode for lukechilds/zsh-nvm
zinit light lukechilds/zsh-nvm

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh


source ~/.torusrc

# To customize prompt, run `p10k configure` or edit ~/.dotfiles/zsh/p10k.zsh.symlink.
[[ ! -f ~/.dotfiles/zsh/p10k.zsh.symlink ]] || source ~/.dotfiles/zsh/p10k.zsh.symlink
